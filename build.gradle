buildscript {
    repositories {
        flatDir {
            dirs 'libs'
        }
    }
    dependencies {
        classpath "systems.danger:danger-plugin-installer:0.1-alpha"
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'org.jetbrains.dokka' version '1.4.32'
    id 'maven-publish'
}

apply plugin: 'danger-kotlin-plugin-installer'

group 'com.rojanthomas'
version '0.1.0'

dangerPlugin {
    outputJar = "${buildDir}/libs/danger-kotlin-jira-0.1.0.jar"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "systems.danger:danger-kotlin-sdk:1.2"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation("io.mockk:mockk:1.11.0")
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    // For pure Kotlin libraries, in case you have them
    from sourceSets.main.java.srcDirs
    from sourceSets.main.kotlin.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    // https://docs.gradle.org/current/userguide/publishing_maven.html
    publications {
        mavenJava(MavenPublication) {

            artifact("$buildDir/libs/${project.getName()}-${version}.jar")
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Danger Kotlin Jira Plugin'
                description = 'A JIRA plugin for Danger Kotlin'
                url = 'https://github.com/rojanthomas/danger-kotlin-jira'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/rojanthomas/danger-kotlin-jira/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'rojanthomas'
                        name = 'Rojan Thomas'
                        email = 'rojanthomas@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/rojanthomas/danger-kotlin-jira.git'
                    developerConnection = 'scm:git:ssh://github.com/rojanthomas/danger-kotlin-jira.git'
                    url = 'https://github.com/rojanthomas/danger-kotlin-jira'
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/rojanthomas/danger-kotlin-jira"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}